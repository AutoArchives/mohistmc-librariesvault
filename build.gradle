plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.mohistmc'
version = '1.20.1'

repositories {
    mavenCentral()
    maven { url 'https://maven.mohistmc.com/' }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

dependencies {
    implementation 'org.yaml:snakeyaml:2.0' // 1.20.1+
    // implementation 'org.yaml:snakeyaml:1.30'  // 1.18.2-1.19.4
    implementation 'javax.inject:javax.inject:1'
}

shadowJar {
    archiveClassifier = null
    dependencies {
        include(dependency('org.yaml:snakeyaml:2.0'))
        include(dependency('javax.inject:javax.inject:1'))
    }
    relocate("org.yaml.snakeyaml", "com.mohistmc.org.yaml.snakeyaml")
    relocate("javax.inject", "com.mohistmc.javax.inject")
}

shadowJar.dependsOn(jar)

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name.toLowerCase()
            version project.version
            from components.java
        }
    }

    repositories {
        mavenLocal()

        maven {
            name = "Mohist-Maven"
            setUrl("file:/Mohist-Maven/maven/")
        }
    }
}